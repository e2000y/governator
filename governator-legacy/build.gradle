plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

apply plugin: "com.github.johnrengelman.shadow"

// Add a new configuration 'bundle' for fine grain control on what artifacts to shadow.
configurations {
    bundle
    api.extendsFrom bundle
}

dependencies {
    api    project(':governator-api')
    api    project(':governator-core')
    api    'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    api    "javax.xml.bind:jaxb-api:${jaxb_version}"
    api    "com.google.inject.extensions:guice-grapher:${guice_version}"

    // ASM is being shaded
    bundle    'org.ow2.asm:asm:9.2'

    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'com.tngtech.java:junit-dataprovider:1.11.0'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation "com.google.code.findbugs:jsr305:3.0.0"
}

shadowJar {
    classifier = null
    configurations = [project.configurations.bundle]

    exclude 'module-info.class'
    relocate 'org.objectweb.asm', 'com.netflix.governator.asm'
}

jar {
    enabled = false
}
jar.dependsOn shadowJar

// Remove asm from published pom file
afterEvaluate {
    publishing {
        publications {
            // to remove shaded dependency from pom.xml
            withType(MavenPublication) {
                pom.withXml {
                    asNode().dependencies.dependency.findAll {
                        it.artifactId.text() == "asm"
                    }
                    .each {
                        it.parent().remove(it)
                    }
                }
            }
        }
    }
}
